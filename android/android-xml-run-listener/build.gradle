apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/schroepf/TestLab/tree/master/android/android-xml-run-listener'
def gitUrl = 'https://github.com/schroepf/TestLab'
def artifactGroup = 'de.schroepf'
def artifactIdentifier = 'android-xml-run-listener-legacy'
def artifactName = "$artifactGroup:$artifactIdentifier"

// This is the library version used when deploying the artifact
group = artifactGroup
version = "0.4.0"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 4
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name artifactName
                groupId artifactGroup
                artifactId artifactIdentifier
                description = 'JUnit XML RunListener for android instrumentation tests (for non AndroidX projects)'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Unlicense'
                        url 'http://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id 'schroepf'
                        name 'Tobias SchrÃ¶pf'
                        email 'schroepf@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.bootClasspath.join(File.pathSeparator))

    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompile.classpath
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'schroepf'
        name = artifactName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation ("com.android.support.test:runner:$test_runner_version") {
        exclude module: 'support-annotations'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
}
